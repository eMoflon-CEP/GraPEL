grammar org.emoflon.cep.Grapel with org.eclipse.xtext.common.Terminals

import "platform:/resource/org.emoflon.ibex.gt.editor/model/generated/GT.ecore" as GT
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate grapel "http://www.emoflon.org/cep/Grapel"

Model:
	(events+=Event | patterns+=GTPattern)*
;

GenericEvent:
	GTPattern | Event
;

Event:
	'event' name=ID '{'
	(patterns += GenericEvent)*
	(contextConstraints += ContextConstraint)*
	(temporalDependencies += TemporalDependency (relation += DependencyRelation temporalDependencies += TemporalDependency)*)?
	'}'
;

GTPattern:
	literal = [GT::EditorPattern]
;

ContextConstraint:
	(lhs = [GT::EditorExpression] | lhs = [GT::EditorReference])
	relation = ContextRelation
	(rhs = [GT::EditorExpression] | rhs = [GT::EditorReference])
;

enum ContextRelation:
	EQUAL='==' |
  UNEQUAL='!=' 
;

TemporalDependency:
	dependency += GenericEvent ('->' dependency += GenericEvent)*
	('{'
		constraints += DependencyConstraint (relation += ConstraintRelation constraints += DependencyConstraint)*
	'}')?
;

enum DependencyRelation:
	AND='&&' |
  OR='||' 
;

DependencyConstraint:
	lhs = ConstraintExpression
	relation = ExpressionRelation
	rhs = ConstraintExpression
;

enum ConstraintRelation:
	AND='&&' |
  OR='||' 
;

enum ExpressionRelation:
  GREATER='>' |
  GREATER_OR_EQUAL='>=' |
  EQUAL='==' |
  UNEQUAL='!=' |
  SMALLER_OR_EQUAL='<=' |
  SMALLER='<'
;

ConstraintExpression:
	lhs=[GT::EditorExpression]
;
