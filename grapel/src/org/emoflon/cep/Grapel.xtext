grammar org.emoflon.cep.Grapel with org.emoflon.ibex.gt.editor.GT

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.emoflon.org/ibex/gt/editor/GT" as GT

generate grapel "http://www.emoflon.org/cep/Grapel"  

@Override 
EditorGTFile: {EditorGTFile} 
	(imports+=EditorImport)*
  	(patterns+=EditorPattern |
  	conditions+=EditorCondition |
  	attributeConditionLibraries+=EditorAttributeConditionLibrary |
  	events += Event |
  	evenPatterns += EventPattern)*
  	
;

Event:
	'event' name=ID '{'
	(attributes += EventAttribute)*
	'}'
;

EventAttribute:
	type=[ecore::EDataType] | type=[ecore::EClass] name=ID ';' 
;

EventPattern:
	'event_pattern' name=ID ('spawns' returnType=[Event|ID])?'{'
	(nodes += EventPatternNode)*
	(contextConstraints += EventPatternContextConstraint)*
	(relationalConstraints += EventPatternRelationalConstraint 
		(relConstraintRelations += RelationalConstraintRelation relationalConstraints += EventPatternRelationalConstraint)*
	)?
	
	('spawn' returnArg=[Event|ID]'('
		(returnParams+=EventPatternNodeAttributeExpression 
			(',' returnParams+=EventPatternNodeAttributeExpression)* 
		)?
	')')?
	
	'}'
;

EventPatternNode:
	name=ID ':' type=[Event|ID] | type=[GT::EditorPattern|ID]
;

EventPatternNodeAttributeExpression:
	(attributeExpression = [EventPatternNode|ID] '.') (attribute = [EventAttribute|ID] ('->' field = [ecore::EAttribute|ID])?) | (attribute = [GT::EditorNode|ID] ('->' field = [ecore::EAttribute|ID])?)
;

EventPatternContextConstraint:
	lhs = EventPatternNodeAttributeExpression
	relation = ContextRelation
	rhs = EventPatternNodeAttributeExpression
;

enum ContextRelation:
	EQUAL='==' |
  UNEQUAL='!=' 
;

enum RelationalConstraintRelation:
	AND='&&' |
  	OR='||' 
;

EventPatternRelationalConstraint:
	operands +=  [EventPatternNode|ID] (operators += RelationalOperator operands += [EventPatternNode|ID])* '{'
		(constraints += AttributeConstraint (relations += AttributeConstraintRelation constraints += AttributeConstraint)*)?
	'}'
;

enum RelationalOperator:
	AND='&&' |
  	OR='||' |
  	FOLLOWS='->' 
;

AttributeConstraint:
	operands += EventPatternNodeAttributeExpression (relations += AttributeRelation operands += EventPatternNodeAttributeExpression)*
;

enum AttributeConstraintRelation:
  AND='&&' |
  	OR='||' 
;

enum AttributeRelation:
  GREATER='>' |
  GREATER_OR_EQUAL='>=' |
  EQUAL='==' |
  UNEQUAL='!=' |
  SMALLER_OR_EQUAL='<=' |
  SMALLER='<'
;
